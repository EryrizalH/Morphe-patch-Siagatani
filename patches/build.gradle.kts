group = "app.siagatani"

patches {
    about {
        name = "Siaga Tani Patches"
        description = "Morphe patches for Siaga Tani attendance app"
        source = "git@github.com:EryrizalH/Morphe-patch-Siagatani.git"
        author = "EryrizalH"
        contact = "https://github.com/EryrizalH"
        website = "https://github.com/EryrizalH/Morphe-patch-Siagatani"
        license = "GPLv3"
    }
}

kotlin {
    compilerOptions {
        freeCompilerArgs.add("-Xcontext-parameters")
    }
}

// Separate configuration so gson is available at runtime for the
// generatePatchesList task but never bundled into the APK.
val patchListGeneratorClasspath: Configuration by configurations.creating

dependencies {
    compileOnly(libs.gson)
    patchListGeneratorClasspath(libs.gson)
}

tasks {
    register<JavaExec>("generatePatchesList") {
        description = "Build patch with patch list"

        dependsOn(build)

        classpath = sourceSets["main"].runtimeClasspath + patchListGeneratorClasspath
        mainClass.set("util.PatchListGeneratorKt")
    }

    // Used by gradle-semantic-release-plugin.
    publish {
        dependsOn("generatePatchesList")
    }
}